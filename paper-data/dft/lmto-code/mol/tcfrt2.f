      SUBROUTINE TCFRT2(LX1,NX1,LX2,NX2,NXI,NLM1,NLM2,RHS,SHS,
     .   RMAT,NDIM)
C  DOES THE MATRIX MULTIPLICATIONS TO ROTATE THE TCF.
C  INPUT AND OUTPUT IS RHS. SHS IS WKSPACE, SAME SIZE AS RHS.
C  RMAT FROM TCFRT1. NOTE: COULD BE REWRITTEN TO USE LESS WKSPACE.
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION LX1(1),LX2(1),RMAT(NDIM,NDIM),RHS(NXI,NLM1,NLM2),
     .   SHS(NXI,NLM1,NLM2)
C ---- MULTIPLY RHS BY ROTATION MATRIX FIRST TIME ------
      NNN=NLM1*NLM2
      IXI0=0
      CALL DPZERO(SHS,NXI*NLM1*NLM2)
      CALL XXFRT2(NX1,LX1,RHS,SHS,NXI,NNN,RMAT,NDIM,IXI0)
      CALL XXFRT2(NX2,LX2,RHS,SHS,NXI,NNN,RMAT,NDIM,IXI0)
C ---- MULTIPLY RHS BY ROTATION MATRIX SECOND TIME -----
      CALL DPZERO(RHS,NXI*NLM1*NLM2)
      DO 52 ILM2=1,NLM2
      I1=LL(ILM2)**2+1
      I2=(LL(ILM2)+1)**2
      DO 52 ILM1=1,NLM1
      DO 52 JLM2=I1,I2
      DO 53 IXI=1,NXI
      RHS(IXI,ILM1,ILM2)=RHS(IXI,ILM1,ILM2)
     .       +RMAT(JLM2,ILM2)*SHS(IXI,ILM1,JLM2)
  53  CONTINUE
  52  CONTINUE
C ---- MULTIPLY RHS BY ROTATION MATRIX THIRD TIME ------
      CALL DPZERO(SHS,NXI*NLM1*NLM2)
      DO 54 ILM1=1,NLM1
      I1=LL(ILM1)**2+1
      I2=(LL(ILM1)+1)**2
      DO 54 ILM2=1,NLM2
      DO 54 JLM1=I1,I2
      DO 55 IXI=1,NXI
      SHS(IXI,ILM1,ILM2)=SHS(IXI,ILM1,ILM2)
     .       +RMAT(JLM1,ILM1)*RHS(IXI,JLM1,ILM2)
  55  CONTINUE
  54  CONTINUE
C ---- COPY RESULT TO RHS -----------------
      CALL DPCOPY(SHS,RHS,1,NLM1*NLM2*NXI,1.D0)

      RETURN
      END
C ------- XXFRT2 ---------------------------
      SUBROUTINE XXFRT2(NX1,LX1,RHS,SHS,NXI,NNN,RMAT,NDIM,IXI0)
C  THIS PART IS SPLIT OFF INTO A SUBROUTINE TO PERMIT OPERATIONS
C  OVER VECTORS OF LENGTH NLM1*NLM2=NNN.
      IMPLICIT REAL*8 (A-H,P-Z)
      DIMENSION SHS(NXI,NNN),RHS(NXI,NNN),
     .   LX1(1),RMAT(NDIM,NDIM)
      DO 5 IE=1,NX1
      LXX=LX1(IE)
      NLMXI=(LXX+1)**2
      DO 51 ILMXI=1,NLMXI
      I1=LL(ILMXI)**2+1
      I2=(LL(ILMXI)+1)**2
      DO 51 JLMXI=I1,I2
      DO 50 III=1,NNN
      SHS(ILMXI+IXI0,III)=SHS(ILMXI+IXI0,III)
     .   +RMAT(JLMXI,ILMXI)*RHS(JLMXI+IXI0,III)
  50  CONTINUE
  51  CONTINUE
  5   IXI0=IXI0+NLMXI
      RETURN
      END
