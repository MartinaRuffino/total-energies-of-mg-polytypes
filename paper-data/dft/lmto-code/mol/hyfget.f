      SUBROUTINE HYFGET(DR,R1,E1,NLM1,R2,E2,NLM2,X1,X2,NF1,NF2,
     .   NXI1,LXI1,EXI1,NXI2,LXI2,EXI2,TSPEC,TDATA)
C  SEARCHES THROUGH TSPEC TO FIND THE CORRECT TABLE, THEN GETS OUT
C  THE TABULATED 2-CENTER EXPANSION. EXPANSION COEFFICIENTS
C  FOR ALL NLM1*NLM2 PRODUCTS ARE RETURNED IN X1 AND X2.
      IMPLICIT REAL*8 (A-H,P-Z), INTEGER (o)
      DIMENSION TSPEC(100,1),LXI1(1),LXI2(1),EXI1(1),EXI2(1),
     .   JXI1(10),JXI2(10),TDATA(1),DR(3),LX1(10),LX2(10),
     .   X1(NF1,NLM1,NLM2),X2(NF2,NLM2,NLM1),DIR(3)
      REAL W(1)
      COMMON /W/ W
      CALL GETPR(IPR)
      L1=LL(NLM1)
      L2=LL(NLM2)
C ------- LOCATE THE CORRECT TABLE -----------------
      CALL HYFLOC(R1,L1,E1,R2,L2,E2,NXI1,LXI1,EXI1,
     .   NXI2,LXI2,EXI2,TSPEC,ITBL,JXI1,JXI2)
      IF(ITBL.EQ.0) CALL RX('HYFGET:  NO SUITABLE TABLE FOUND''')
      IT=IABS(ITBL)
      NDT0=IDNINT(TSPEC(25,IT))
      IF(IPR.GE.50) WRITE(6,650) R1,R2,L1,L2,E1,E2,ITBL,NDT0
  650 FORMAT(' HYFGET:   R=',2F8.3,'   L=',2I3,'   E=',2F9.4,
     .   /' ITBL=',I3,'    DATA OFFSET=',I10)
C ------- GET SPECIFICATIONS OF TABLE ---------
      D0  =       TSPEC( 3,IT)
      A1  =       TSPEC( 5,IT)
      NALF=IDNINT(TSPEC( 7,IT))
      MMAX=IDNINT(TSPEC( 8,IT))
      NCOF=IDNINT(TSPEC( 9,IT))
      NXI =IDNINT(TSPEC(12,IT))
      LSYM=IDNINT(TSPEC(13,IT))
      LP1 =IDNINT(TSPEC(15,IT))
      LP2 =IDNINT(TSPEC(17,IT))
      NX1 =IDNINT(TSPEC(19,IT))
      NX2 =IDNINT(TSPEC(20,IT))
      DO 14 I=1,NX1
  14  LX1(I)=IDNINT(TSPEC(40+I,IT))
      DO 15 I=1,NX2
  15  LX2(I)=IDNINT(TSPEC(60+I,IT))
C ------- PRINTOUT ---------------------
      IF(IPR.GE.60) THEN
      WRITE(6,600) D0,A1,NALF,NCOF,MMAX,NXI,LSYM
  600 FORMAT(' D0=',F8.4,'   A1=',F8.4,'   NALF=',I3,'   NCOF=',I6
     .   /' MMAX=',I3,'    NXI=',I5,'    LSYM=',I2)
      WRITE(6,601) LP1,NX1,(LX1(K),K=1,NX1)
      WRITE(6,601) LP2,NX2,(LX2(K),K=1,NX2)
  601 FORMAT(' LP=',I2,'   NX=',I2,'   LX=',8I5)
      ENDIF
C ------- SOME SETUP -------------
      MLM1=(LP1+1)**2
      MLM2=(LP2+1)**2
      LROT=MAX0(LP1,LP2)
      DO 4 I=1,NX1
  4   LROT=MAX0(LROT,LX1(I))
      DO 5 I=1,NX2
  5   LROT=MAX0(LROT,LX2(I))
      NDROT=(LROT+1)**2
      DO 22 M=1,3
      DIR(M)=DR(M)
  22  IF(ITBL.LT.0) DIR(M)=-DR(M)
      CALL DEFRR(oA,   NXI*MLM1*MLM2)
C ------- EVALUATE INTERPOLATION AT DISTANCE D ------------
      CALL DEFRR(oCOF,    NCOF)
      D=DSQRT(DR(1)**2+DR(2)**2+DR(3)**2)
      CALL HYFIPL(D,D0,A1,TDATA(NDT0),W(oCOF),NCOF,NALF)
      CALL TCFXPN(MMAX,LX1,NX1,LX2,NX2,LP1,LP2,LSYM,W(oCOF),
     .   W(oA),NXI,MLM1,MLM2)
      CALL RLSE(oCOF)
C ------- ROTATE TO DIRECTION OF DIR ------------
      CALL DEFRR(oB,      NXI*MLM1*MLM2)
      CALL DEFRR(oRMAT,   NDROT*NDROT)
      CALL TCFRT1(DIR,W(oRMAT),LROT,NDROT)
      CALL TCFRT2(LX1,NX1,LX2,NX2,NXI,MLM1,MLM2,W(oA),W(oB),
     .   W(oRMAT),NDROT)
      CALL RLSE(oB)
C ------- COPY OVER INTO X1,X2 -----------------
      IF(ITBL.GT.0) THEN
      CALL XXFGET(W(oA),NXI,MLM1,MLM2,X1,X2,NF1,NF2,NLM1,NLM2,
     .   NXI1,NXI2,LXI1,LXI2,NX1,NX2,LX1,LX2,JXI1,JXI2)
      ELSE
      CALL XXFGET(W(oA),NXI,MLM1,MLM2,X2,X1,NF2,NF1,NLM2,NLM1,
     .   NXI2,NXI1,LXI2,LXI1,NX1,NX2,LX1,LX2,JXI2,JXI1)
      ENDIF
      CALL RLSE(oA)

      RETURN
      END
C -------- SUB XXFGET --------------------------
      SUBROUTINE XXFGET(A,NXI,MLM1,MLM2,X1,X2,NF1,NF2,NLM1,NLM2,
     .   NXI1,NXI2,LXI1,LXI2,NX1,NX2,LX1,LX2,JXI1,JXI2)
C  COPIES THE FINAL TCF OVER TO ARRAYS X1,X2.
      IMPLICIT REAL*8 (A-H,P-Z), INTEGER (o)
      DIMENSION X1(NF1,NLM1,NLM2),X2(NF2,NLM2,NLM1),JXI1(10),JXI2(10),
     .   A(NXI,MLM1,MLM2),LX1(1),LX2(1),LXI1(1),LXI2(1),J1(10),J2(10)
      CALL DPZERO(X1,NF1*NLM1*NLM2)
      CALL DPZERO(X2,NF2*NLM1*NLM2)
C -------- SET UP OFFSETS FOR X1 AND X2 -------
      J1(1)=0
      DO 1 J=2,NXI1
  1   J1(J)=J1(J-1)+(LXI1(J-1)+1)**2
      J2(1)=0
      DO 2 J=2,NXI2
  2   J2(J)=J2(J-1)+(LXI2(J-1)+1)**2
C -------- START LOOPING OVER COEFFS IN A -----------
      DO 10 ILM1=1,NLM1
      DO 10 ILM2=1,NLM2
      II0=0
      DO 5 I=1,NX1
      J=JXI1(I)
      JJ0=J1(J)
      NLMI=(LX1(I)+1)**2
      DO 6 ILM=1,NLMI
  6   X1(JJ0+ILM,ILM1,ILM2)=A(II0+ILM,ILM1,ILM2)
  5   II0=II0+NLMI
      DO 7 I=1,NX2
      J=JXI2(I)
      JJ0=J2(J)
      NLMI=(LX2(I)+1)**2
      DO 8 ILM=1,NLMI
  8   X2(JJ0+ILM,ILM2,ILM1)=A(II0+ILM,ILM1,ILM2)
  7   II0=II0+NLMI

  10  CONTINUE
      RETURN
      END
