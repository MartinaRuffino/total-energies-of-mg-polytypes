#!/bin/csh -f

# shell script to create MPIK version of library subs.a

# This script requires environment variable F90m is set,
# which is the MPI f90 compiler and associated flags

# Also ccomp must be in your path, or environment variable
# CCOMP must point to it.

# checks before script execution

if (! $?CCOMP) then
  which ccomp >/dev/null
  if ($status != 0) then
    echo Exit -1 subs-to-mpik: executable ccomp apparently not in path.
    echo Set environment variable CCOMP to ccomp preprocessor before executing this script
    exit -1
  endif
  set ccomp = `which ccomp`
else
  set ccomp = ($CCOMP)
  which $ccomp[1] >/dev/null
  if ($status != 0) then
    echo Exit -1 subs-to-mpik: environment variable CCOMP=$ccomp[1] does not correspond to executable in your path.
    echo Set environment variable CCOMP to a valid ccomp preprocessor before executing this script.
    exit -1
  endif
endif

if (! $?F90M) then
  echo Exit -1 subs-to-mpik: no environment variable F90M defined.
  echo "Set environment variable F90M to your MPI-f90 compiler path (followed by appropriate switches) before executing this script"
  exit -1
endif
set fc = ($F90M)
which $fc[1] >/dev/null
if ($status != 0) then
  echo Exit -1 subs-to-mpik: compiler "$fc[1]" apparently not in path.
  echo Set environment variable F90M to compiler name followed by switches before executing this script
  exit -1
endif

# For debugging
# set fc = (`echo $fc  | sed s'/O3/O0 -g/'`)

set less2
if ($?MACHINE == 1) then
  set machconfig = `../misc/config.guess` 
  set less2 = `./Special-Flags $machconfig $MACHINE | grep FFLAGS_LESS2 | sed s'/$(FC) $(FFLAGS_LESS2) :://'`
  set fless2 = `egrep ^FFLAGS_LESS2 Make.inc  | sed 's/FFLAGS_LESS2 =//' | sed s/.serial//`
endif

set lst = `grep -il 'use mpi' *.f *.f90 *.F90 | grep -v nullmpi`
if (-e mpilist) then
set lst = ($lst `cat mpilist`)
endif

echo ... compiling $lst

cp subs.a subs-MPIK.a
foreach i ( $lst )
   echo ... making MPI version of file $i
   ar dv subs-MPIK.a $i:r.o
   echo $fc -c $i
        $fc -c $i
   echo $less2 | grep $i:r >/dev/null
   if ($status == 0) then
     echo $fc[1] $fless2 -c $i
          $fc[1] $fless2 -c $i
   endif
   ar rv subs-MPIK.a $i:r.o
   rm $i:r.o
end
