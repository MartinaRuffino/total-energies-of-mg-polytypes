#!/bin/csh -f

# shell script to create MPI version of library subs.a

# This script requires environment variable F90m is set,
# which is the MPI f90 compiler and associated flags

# Also ccomp must be in your path, or environment variable
# CCOMP must point to it.

# checks before script execution

if (! $?CCOMP) then
  which ccomp >/dev/null
  if ($status != 0) then
    echo Exit -1 subs-to-mpi: executable ccomp apparently not in path.
    echo Set environment variable CCOMP to ccomp preprocessor before executing this script
    exit -1
  endif
  set ccomp = `which ccomp`
else
  set ccomp = ($CCOMP)
  which $ccomp[1] >/dev/null
  if ($status != 0) then
    echo Exit -1 subs-to-mpi: environment variable CCOMP=$ccomp[1] does not correspond to executable in your path.
    echo Set environment variable CCOMP to a valid ccomp preprocessor before executing this script.
    exit -1
  endif
endif

if (! $?F90M) then
  echo Exit -1 subs-to-mpi: no environment variable F90M defined.
  echo "Set environment variable F90M to your MPI-f90 compiler path (followed by appropriate switches) before executing this script"
  exit -1
endif
set fc = ($F90M)
which $fc[1] >/dev/null
if ($status != 0) then
  echo Exit -1 subs-to-mpi: compiler "$fc[1]" apparently not in path.
  echo Set environment variable F90M to compiler name followed by switches before executing this script
  exit -1
endif

set less2
if ($?MACHINE == 1) then
  set machconfig = `../misc/config.guess` 
  set less2 = `./Special-Flags $machconfig $MACHINE | grep FFLAGS_LESS2 | sed s'/$(FC) $(FFLAGS_LESS2) :://'`
  set fless2 = `egrep ^FFLAGS_LESS2 Make.inc  | sed 's/FFLAGS_LESS2 =//' | sed s/.serial//`
endif

echo "subs-to-mpi: compiling with $fc -c"
echo "             generate MPI versions of subroutines with $ccomp"

set lst = (`egrep -l '^[Cc]#.*if.*MPI' *.f`)
if (-e mpilist) then
set lst = (`egrep -l '^[Cc]#.*if.*MPI' *.f` `cat mpilist`)
endif

echo ... compiling $lst

cp subs.a subs-MPI.a
foreach i ( $lst )
   echo ... making MPI version of file $i
   ar dv subs-MPI.a $i:r.o
   echo $ccomp -uMPE -dMPI $i $i:r-MPI.f
        $ccomp -uMPE -dMPI $i $i:r-MPI.f
   echo $fc -c $i:r-MPI.f
        $fc -c $i:r-MPI.f
   echo $less2 | grep $i:r >/dev/null
   if ($status == 0) then
     echo $fc $fless2 -c $i:r-MPI.f
          $fc $fless2 -c $i:r-MPI.f
   endif
   rm $i:r-MPI.f
   ar rv subs-MPI.a $i:r-MPI.o
   rm $i:r-MPI.o
end
