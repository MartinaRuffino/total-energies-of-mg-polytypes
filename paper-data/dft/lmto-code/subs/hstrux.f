      subroutine hstrux(e,nlma,nlmb,nlmx,npow,n1,n2,ikl,jkl,ip,cf,hl,s)
C- Vectorized strux and energy derivatives
C ----------------------------------------------------------------------
Ci Inputs
Ci   e     :energy
Ci   nlma  :Generate S_R'L',RL for L' < nlma
Ci   nlmb  :Generate S_R'L',RL for L  < nlmb
Ci   nlmx  :leading dimension of ikl,jkl,hl; generated by setup strxsu
Ci   npow  :generated by setup strxsu
Ci   n1,n2 :make strux for reduced strux hl(*,n1..n2)
Ci   ikl   :generated by setup strxsu
Ci   jkl   :generated by setup strxsu
Ci   ip    :generated by setup strxsu
Ci   cf    :generated by setup strxsu
Ci   hl    :reduced strux (Hankels for connecting vector; see rstr0.f)
Co Outputs
Co   s     :real-space structure constants
Cr Remarks
Cr    Expansion theorem is:  k(k,r-dr) = sum(m) s(m,k,dr)*j(m,r)
Cr    See mstrx2 for nonvectorized version
Cr
Cr    hstrux requires call to setup routine strxsu, and hl; see rstr0
Cu Updates
C ----------------------------------------------------------------------
      implicit none
C ... Passed parameters
      integer nlma,nlmb,nlmx,npow,n1,n2
      integer ip(*),ikl(nlmx,0:*),jkl(nlmx,0:*)
      double precision e,hl(nlmx,n2),cf(*),s(nlma*nlmb,1),
     .  efac(0:20)
C ... Local parameters
      integer i,i1,i2,ilm,ipow,k,l,ll,lmax,nlm,n,m
      double precision fpi,xx(4)
c     parameter (tol=1d-12)

C --- Setup ---
      fpi = 16d0*datan(1d0)
      lmax = ll(nlma) + ll(nlmb)
      nlm = (lmax+1)**2
      if (lmax > 20) call rx('hstrux: lmax gt 20')
      efac(0) = 1d0 * fpi
C     edot(0) = 0d0
      do  l = 1, lmax
        efac(l) = -e*efac(l-1)
C       edot(l) = -l*efac(l-1)
      enddo

C --- Loop over ipow,ilm ---
      call dpzero(s, nlma*nlmb*(n2-n1+1))
      do  n = n1, n2, 2
        m = n-n1+1
        if (n == n2) then
          do  ipow = 0, npow
            do  ilm = 1, nlm
            i1 = ikl(ilm,ipow)
            i2 = jkl(ilm,ipow)
            xx(1) = efac(ipow)*hl(ilm,n)
              do  i = i1, i2
              k = ip(i)
              s(k,m) = s(k,m) + xx(1)*cf(i)
              enddo
            enddo
          enddo
        else
          do  ipow = 0, npow
            do  ilm = 1, nlm
            i1 = ikl(ilm,ipow)
            i2 = jkl(ilm,ipow)
            xx(1) = efac(ipow)*hl(ilm,n)
            xx(2) = efac(ipow)*hl(ilm,n+1)
              do  i = i1, i2
              k = ip(i)
              s(k,m)   = s(k,m)   + xx(1)*cf(i)
              s(k,m+1) = s(k,m+1) + xx(2)*cf(i)
              enddo
            enddo
          enddo
        endif
      enddo
      end
      subroutine hstrud(e,nlma,nlmb,nlmx,npow,offh,ikl,jkl,ip,cf,hl,hd,
     .  s,sd)
C- Vectorized strux and energy derivatives
C ----------------------------------------------------------------------
Ci Inputs
Ci   e     :energy
Ci   nlma  :Generate S_R'L',RL for L' < nlma
Ci   nlmb  :Generate S_R'L',RL for L  < nlmb
Ci   nlmx  :leading dimension of ikl,jkl; generated by setup strxsu
Ci   npow  :generated by setup strxsu
Ci   offh  :offset to the reduced strux hl and hd.
Ci   ikl   :generated by setup strxsu
Ci   jkl   :generated by setup strxsu
Ci   ip    :generated by setup strxsu
Ci   cf    :generated by setup strxsu
Ci   hl    :reduced strux (Hankels for connecting vector; see rstr0.f)
Ci   hd    :energy derivative of hl
Co Outputs
Co   s     :real-space structure constants
Co   sd    :energy derivative of s
Cr Remarks
Cr    Exp. theorem is:  k(k,r-dr) = sum(m) s(m,k,dr)*j(m,r)
Cr    See mstrx2 for nonvectorized version
Cr
Cr    hstrud requires call to setup routine strxsu, and hl,hd; see rstr0
Cu Updates
C ----------------------------------------------------------------------
      implicit none
C ... Passed parameters
      integer nlma,nlmb,nlmx,npow,offh
      integer ip(*),ikl(nlmx,0:*),jkl(nlmx,0:*)
      double precision e,hl(*),hd(*),cf(*),s(nlma*nlmb),sd(nlma*nlmb)
C ... Local parameters
      integer i,i1,i2,ilm,ipow,k,l,ll,lmax,nlm
      double precision fpi,xxd,xxx,efac(0:20),edot(0:20)
c     parameter (tol=1d-12)

C --- Setup ---
      fpi = 16d0*datan(1d0)
      lmax = ll(nlma) + ll(nlmb)
      nlm = (lmax+1)**2
      if (lmax > 20) call rx('hstrud: lmax gt 20')
      efac(0) = 1d0 * fpi
      edot(0) = 0d0
      do  l = 1, lmax
        efac(l) = -e*efac(l-1)
        edot(l) = -l*efac(l-1)
      enddo

C --- Loop over ipow,ilm ---
      call dpzero(s, nlma*nlmb)
      call dpzero(sd,nlma*nlmb)
      do  ipow = 0, npow
        do  ilm = 1, nlm
        i1 = ikl(ilm,ipow)
        i2 = jkl(ilm,ipow)
        xxx = efac(ipow)*hl(ilm+offh)
        xxd = efac(ipow)*hd(ilm+offh) + edot(ipow)*hl(ilm+offh)
C       if(dabs(xxx) > tol .or. (dabs(xxd) > tol)) then
          do  i = i1, i2
            k = ip(i)
C            s(k,1) = s(k,1)  + xxx*cf(i)
C            sd(k,1)= sd(k,1) + xxd*cf(i)
            s(k) = s(k)  + xxx*cf(i)
            sd(k)= sd(k) + xxd*cf(i)
          enddo
C        endif
        enddo
      enddo
      end
